"""
    is_feasible(x, milp[; cons_tol=1e-6, int_tol=1e-5, verbose=true])

Check whether solution vector `x` is feasible for `milp`, returning one verdict per column if `x` holds a batch of solutions.

# Keyword arguments

- `cons_tol`: tolerance for constraint satisfaction
- `int_tol`: tolerance for integrality requirements
- `verbose`: whether to display warnings
"""
function is_feasible(x::AbstractMatrix, milp::MILP; kwargs...)
    return map(axes(x, 2)) do i
        is_feasible(view(x, :, i), instance(milp, i); kwargs...)
    end
end

function is_feasible(
        x::AbstractVector, milp::MILP;
        cons_tol = 1.0e-6, int_tol = 1.0e-5, verbose::Bool = true
    )
    (; lv, uv, A, lc, uc, int_var) = milp
    # a problem may have no constraint and no integer variable at all
    none = typemin(eltype(x))
    bounds_err = max(maximum(x - uv; init = none), maximum(lv - x; init = none))
    cons_err = max(maximum(A * x - uc; init = none), maximum(lc - A * x; init = none))
    xint = x[int_var]
    int_err = maximum(abs, xint .- round.(Int, xint); init = zero(eltype(x)))
    if bounds_err > cons_tol
        verbose && @warn "Variable bounds not satisfied" bounds_err cons_tol
        return false
    elseif cons_err > cons_tol
        verbose && @warn "Constraints not satisfied" cons_err cons_tol
        return false
    elseif int_err > int_tol
        verbose && @warn "Integrality not satisfied" int_err int_tol
        return false
    else
        return true
    end
end

"""
    objective_value(x, milp)

Compute the value of the objective of `milp` at solution vector `x`, constant included.
"""
objective_value(x, milp::MILP) = coldot(x, milp.c) .+ milp.c0

"""
    PrimalDualSolution

# Fields

$(TYPEDFIELDS)
"""
mutable struct PrimalDualSolution{T <: Number, V <: AbstractVecOrMat{T}}
    "primal solution"
    const x::V
    "dual solution"
    const y::V
end

Base.eltype(::PrimalDualSolution{T}) where {T} = T

function Base.copy(z::PrimalDualSolution)
    return PrimalDualSolution(
        copy(z.x),
        copy(z.y),
    )
end

function Base.zero(z::PrimalDualSolution{T}) where {T}
    return PrimalDualSolution(
        zero(z.x),
        zero(z.y),
    )
end

function zero!(z::PrimalDualSolution{T}) where {T}
    zero!(z.x)
    zero!(z.y)
    return nothing
end

function Base.copy!(z1::PrimalDualSolution, z2::PrimalDualSolution)
    copy!(z1.x, z2.x)
    copy!(z1.y, z2.y)
    return z1
end

function LinearAlgebra.axpby!(
        a::BatchedNumber, x::PrimalDualSolution{T, V}, b::BatchedNumber, y::PrimalDualSolution{T, V},
    ) where {T, V}
    colaxpby!(a, x.x, b, y.x)
    colaxpby!(a, x.y, b, y.y)
    return y
end

# broadcast rather than `axpby!`: the BLAS wrapper boxes its scalars into `Ref`s, which
# only the optimizer removes, so `LinearAlgebra.axpby!` allocates at reduced optimization
function colaxpby!(
        a::BatchedNumber, x::AbstractVecOrMat, b::BatchedNumber, y::AbstractVecOrMat
    )
    ar, br = transpose(a), transpose(b)
    broadcast!!(y, ar, x, br, y) do a, x, b, y
        a * x + b * y
    end
    return y
end

"""
    batched_select!(sol, cond, sol_other)

Overwrite the columns of `sol` for which `cond` holds with those of `sol_other`.
"""
function batched_select!(
        sol::PrimalDualSolution, cond::BatchedNumber,
        sol_other::PrimalDualSolution,
    )
    condr = transpose(cond)
    broadcast!!(ifelse, sol.x, condr, sol_other.x, sol.x)
    broadcast!!(ifelse, sol.y, condr, sol_other.y, sol.y)
    return sol
end

function Base.isapprox(sol1::PrimalDualSolution, sol2::PrimalDualSolution; kwargs...)
    return isapprox(sol1.x, sol2.x; kwargs...) && isapprox(sol1.y, sol2.y; kwargs...)
end

"""
    PrimalDualSolution(milp)

Build the zero solution of `milp`, with one column per instance if `milp` is batched.
"""
function PrimalDualSolution(milp::MILP)
    batched = Val(isbatched(milp))
    nbinst = nbinstances(milp)
    return PrimalDualSolution(
        batched_zeros(milp.lv, nbvar(milp), nbinst, batched),
        batched_zeros(milp.lc, nbcons(milp), nbinst, batched),
    )
end

nbinstances((; x)::PrimalDualSolution) = size(x, 2)
function instance(sol::PrimalDualSolution, i::Int)
    return PrimalDualSolution(
        instance_vec(sol.x, i),
        instance_vec(sol.y, i),
    )
end
