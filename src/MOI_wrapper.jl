MOI.Utilities.@product_of_sets(
    RHS,
    MOI.EqualTo{T},
    MOI.GreaterThan{T},
    MOI.LessThan{T},
    MOI.Interval{T},
)

"""
    Optimizer

Solver type compatible with JuMP, which calls an algorithm from `CoolPDLP` under the hood.

Its options are the same as the keyword arguments of [`Algorithm`](@ref).
"""
mutable struct Optimizer{T} <: MOI.AbstractOptimizer
    x::Vector{T}
    y::Vector{T}
    z::Vector{T}
    obj_value::T
    dual_obj_value::T
    termination_status::MOI.TerminationStatusCode
    primal_status::MOI.ResultStatusCode
    dual_status::MOI.ResultStatusCode
    solve_time::Float64
    silent::Bool
    sets::Union{Nothing, RHS{T}}
    options::Dict{Symbol, Any}

    function Optimizer{T}() where {T <: Real}
        return new{T}(
            T[], T[], T[], T(NaN), T(NaN),
            MOI.OPTIMIZE_NOT_CALLED, MOI.UNKNOWN_RESULT_STATUS, MOI.UNKNOWN_RESULT_STATUS,
            0.0, false, nothing, Dict{Symbol, Any}(),
        )
    end
end

Optimizer() = Optimizer{Float64}()

function MOI.is_empty(model::Optimizer)
    return (
        isempty(model.x) &&
            isempty(model.y) &&
            isempty(model.z) &&
            isnan(model.obj_value) &&
            isnan(model.dual_obj_value) &&
            model.termination_status == MOI.OPTIMIZE_NOT_CALLED &&
            model.primal_status == MOI.UNKNOWN_RESULT_STATUS &&
            model.dual_status == MOI.UNKNOWN_RESULT_STATUS &&
            iszero(model.solve_time) &&
            isnothing(model.sets)
    )
end
function MOI.empty!(model::Optimizer{T}) where {T}
    empty!(model.x)
    empty!(model.y)
    empty!(model.z)
    model.obj_value = T(NaN)
    model.dual_obj_value = T(NaN)
    model.termination_status = MOI.OPTIMIZE_NOT_CALLED
    model.primal_status = MOI.UNKNOWN_RESULT_STATUS
    model.dual_status = MOI.UNKNOWN_RESULT_STATUS
    model.solve_time = 0.0
    model.sets = nothing
    return
end

const SUPPORTED_SET_TYPE{T} = Union{MOI.EqualTo{T}, MOI.GreaterThan{T}, MOI.LessThan{T}, MOI.Interval{T}}

MOI.supports_constraint(::Optimizer{T}, ::Type{MOI.VariableIndex}, ::Type{<:SUPPORTED_SET_TYPE{T}}) where {T} = true
MOI.supports_constraint(::Optimizer{T}, ::Type{MOI.ScalarAffineFunction{T}}, ::Type{<:SUPPORTED_SET_TYPE{T}}) where {T} = true
MOI.supports(::Optimizer{T}, ::MOI.ObjectiveFunction{MOI.ScalarAffineFunction{T}}) where {T} = true

MOI.supports(::Optimizer, ::MOI.ObjectiveSense) = true
MOI.supports(::Optimizer, ::MOI.Silent) = true
MOI.supports(::Optimizer, ::MOI.TimeLimitSec) = true
MOI.supports(::Optimizer, ::MOI.RawOptimizerAttribute) = true

MOI.get(model::Optimizer, ::MOI.TimeLimitSec) = get(model.options, :time_limit, nothing)
function MOI.set(model::Optimizer{T}, ::MOI.TimeLimitSec, value) where {T}
    return if isnothing(value)
        delete!(model.options, :time_limit)
    else
        model.options[:time_limit] = Float64(value)
    end
end
MOI.get(model::Optimizer, ::MOI.Silent) = model.silent
MOI.set(model::Optimizer, ::MOI.Silent, value::Bool) = (model.silent = value;)
MOI.get(model::Optimizer, attr::MOI.RawOptimizerAttribute) = model.options[Symbol(attr.name)]
MOI.set(model::Optimizer, attr::MOI.RawOptimizerAttribute, value) = (model.options[Symbol(attr.name)] = value;)

MOI.get(::Optimizer, ::MOI.SolverName) = "CoolPDLP"
MOI.get(::Optimizer, ::MOI.SolverVersion) = string(pkgversion(CoolPDLP))
MOI.get(model::Optimizer, ::MOI.TerminationStatus) = model.termination_status
MOI.get(model::Optimizer, ::MOI.ResultCount) = model.termination_status != MOI.OPTIMIZE_NOT_CALLED ? 1 : 0
MOI.get(model::Optimizer, ::MOI.RawStatusString) = string(model.termination_status)
MOI.get(model::Optimizer, ::MOI.SolveTimeSec) = model.solve_time

function _status_check_index(model, attr, ret)
    if attr.result_index > MOI.get(model, MOI.ResultCount())
        return MOI.NO_SOLUTION
    end
    return ret
end
function _attr_check_index(model, attr, ret)
    MOI.check_result_index_bounds(model, attr)
    return ret
end

MOI.get(model::Optimizer, attr::MOI.PrimalStatus) = _status_check_index(model, attr, model.primal_status)
MOI.get(model::Optimizer, attr::MOI.DualStatus) = _status_check_index(model, attr, model.dual_status)
MOI.get(model::Optimizer, attr::MOI.ObjectiveValue) = _attr_check_index(model, attr, model.obj_value)
MOI.get(model::Optimizer, attr::MOI.DualObjectiveValue) = _attr_check_index(model, attr, model.dual_obj_value)

function MOI.get(model::Optimizer, attr::MOI.VariablePrimal, vi::MOI.VariableIndex)
    MOI.check_result_index_bounds(model, attr)
    return model.x[vi.value]
end

function MOI.get(
        model::Optimizer{T},
        attr::MOI.ConstraintDual,
        ci::MOI.ConstraintIndex{MOI.ScalarAffineFunction{T}, S},
    ) where {T, S <: SUPPORTED_SET_TYPE{T}}
    MOI.check_result_index_bounds(model, attr)
    return model.y[MOI.Utilities.rows(model.sets, ci)]
end

function MOI.get(
        model::Optimizer{T},
        attr::MOI.ConstraintDual,
        ci::MOI.ConstraintIndex{MOI.VariableIndex, MOI.GreaterThan{T}},
    ) where {T}
    MOI.check_result_index_bounds(model, attr)
    return positive_part(model.z[ci.value])
end

function MOI.get(
        model::Optimizer{T},
        attr::MOI.ConstraintDual,
        ci::MOI.ConstraintIndex{MOI.VariableIndex, MOI.LessThan{T}},
    ) where {T}
    MOI.check_result_index_bounds(model, attr)
    return -negative_part(model.z[ci.value])
end

function MOI.get(
        model::Optimizer{T},
        attr::MOI.ConstraintDual,
        ci::MOI.ConstraintIndex{MOI.VariableIndex, S},
    ) where {T, S <: Union{MOI.EqualTo{T}, MOI.Interval{T}}}
    MOI.check_result_index_bounds(model, attr)
    return model.z[ci.value]
end

const OptimizerCache{T} = MOI.Utilities.GenericModel{
    T,
    MOI.Utilities.ObjectiveContainer{T},
    MOI.Utilities.VariablesContainer{T},
    MOI.Utilities.MatrixOfConstraints{
        T,
        MOI.Utilities.MutableSparseMatrixCSC{T, Int, MOI.Utilities.OneBasedIndexing},
        MOI.Utilities.Hyperrectangle{T}, RHS{T},
    },
}

MOI.default_cache(::Optimizer{T}, ::Type{T}) where {T} = MOI.Utilities.UniversalFallback(OptimizerCache{T}())

function MOI.optimize!(dest::Optimizer{T}, src::MOI.ModelLike) where {T}
    cache = MOI.default_cache(dest, T)
    index_map = MOI.copy_to(cache, src)
    MOI.optimize!(dest, cache)
    return index_map, false
end

function _pass_attributes(dest::Optimizer{T}, cache::MOI.Utilities.UniversalFallback{OptimizerCache{T}}, index_map) where {T}
    MOI.Utilities.pass_attributes(dest, cache, index_map, MOI.get(cache, MOI.ListOfVariableIndices()))

    attrs = MOI.Utilities.ModelFilter(a -> !(a isa MOI.ObjectiveSense || a isa MOI.ObjectiveFunction), cache)
    MOI.Utilities.pass_attributes(dest, attrs, index_map)

    for (F, S) in MOI.get(cache, MOI.ListOfConstraintTypesPresent())
        idxs = MOI.get(cache, MOI.ListOfConstraintIndices{F, S}())
        MOI.Utilities.pass_attributes(dest, cache, index_map, idxs)
    end

    return cache.model
end

function MOI.optimize!(dest::Optimizer{T}, fcache::MOI.Utilities.UniversalFallback{OptimizerCache{T}}) where {T}
    MOI.empty!(dest)
    index_map = MOI.Utilities.identity_index_map(fcache)
    cache = _pass_attributes(dest, fcache, index_map)

    n = cache.constraints.coefficients.n
    max_sense = cache.objective.sense == MOI.MAX_SENSE

    A = convert(SparseMatrixCSC{T, Int}, cache.constraints.coefficients)

    c = zeros(T, n)
    c0 = zero(T)
    if cache.objective.scalar_affine !== nothing
        for term in cache.objective.scalar_affine.terms
            c[term.variable.value] += term.coefficient
        end
        c0 = cache.objective.scalar_affine.constant
    end
    if max_sense  # a `MILP` is a minimization problem
        c .*= -one(T)
        c0 = -c0
    end

    dest.sets = cache.constraints.sets
    lv = cache.variables.lower
    uv = cache.variables.upper
    lc = cache.constraints.constants.lower
    uc = cache.constraints.constants.upper

    milp = MILP(; c, c0, lv, uv, A, lc, uc)

    algorithm = get(dest.options, :algorithm, PDLP)

    float_type = get(dest.options, :float_type, T)
    if float_type !== T
        @warn "Got mismatched float type: solving in $float_type but returning the solution in $T."
    end
    int_type = get(dest.options, :int_type, Int)
    matrix_type = get(dest.options, :matrix_type, SparseMatrixCSC)

    # these four keys configure `optimize!` itself rather than `Algorithm`, so they must stay in
    # `dest.options` (unlike `pop!`) for a second `optimize!` call or a later `MOI.get` to see them
    algorithm_option_keys = (:algorithm, :float_type, :int_type, :matrix_type)
    algo_opts = Dict{Symbol, Any}(:show_progress => !dest.silent)
    for (k, v) in dest.options
        k in algorithm_option_keys && continue
        algo_opts[k] = v
    end
    algo = algorithm(float_type, int_type, matrix_type; algo_opts...)

    sol, stats = solve(milp, algo)

    dest.x = Array(sol.x)
    dest.y = Array(sol.y)
    dest.z = proj_multiplier.(c .- milp.At * dest.y, lv, uv)

    raw_obj = objective_value(dest.x, milp)
    raw_dual_obj = (  # lᵀ|y|⁺ - uᵀ|y|⁻ + lᵥᵀ|z|⁺ - uᵥᵀ|z|⁻ + c0
        sum(safeprod_left.(lc, positive_part.(dest.y)))
            - sum(safeprod_left.(uc, negative_part.(dest.y)))
            + sum(safeprod_left.(lv, positive_part.(dest.z)))
            - sum(safeprod_left.(uv, negative_part.(dest.z)))
            + c0
    )
    dest.obj_value = max_sense ? -raw_obj : raw_obj
    dest.dual_obj_value = max_sense ? -raw_dual_obj : raw_dual_obj
    dest.solve_time = stats.time_elapsed

    cts = termination_status(stats)
    @assert cts != MOI.OPTIMIZE_NOT_CALLED "solve did not reach a terminal status"
    dest.termination_status = cts
    dest.primal_status, dest.dual_status = if cts == MOI.OPTIMAL
        MOI.FEASIBLE_POINT, MOI.FEASIBLE_POINT
    else
        MOI.UNKNOWN_RESULT_STATUS, MOI.UNKNOWN_RESULT_STATUS
    end

    return index_map, false
end
